buildscript {
	ext {
		springBootVersion = '2.1.1.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.liquibase:liquibase-gradle-plugin:2.0.1")
	}
}
//TODO Use the swagger to generate the appropriate code.
//TODO Once the swagger is generated, can start looking into making it work with the database.
//TODO Get an Angular/React client up and running for an initial prototype to demo.

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.liquibase.gradle'

group = 'com.reading.tvirdee.project'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
	
	compile('org.springframework.boot:spring-boot-starter')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile("org.springframework.boot:spring-boot-starter-security")
	compile('com.auth0:auth0-spring-security-api:1.0.0-rc.3')
	compile('io.springfox:springfox-swagger2:2.9.2')
	compile('io.springfox:springfox-core:2.9.2')
	compile('io.springfox:springfox-swagger-ui:2.9.2')
	compile('com.h2database:h2')
	compile('mysql:mysql-connector-java')

	liquibaseRuntime('org.liquibase:liquibase-core')
	liquibaseRuntime('org.liquibase:liquibase-groovy-dsl:2.0.1')
	liquibaseRuntime('org.liquibase.ext:liquibase-hibernate5:3.6')
	liquibaseRuntime("org.springframework.boot:spring-boot:2.0.1.RELEASE")
	liquibaseRuntime('org.springframework:spring-orm:5.0.5.RELEASE')
	liquibaseRuntime('mysql:mysql-connector-java')
	liquibaseRuntime('com.h2database:h2')
	/* Due to a bit of a bug with the liquibase-gradle-plugin, need to explicitly state 
	  src/main as a dependency for the plugin to work correctly. */
	liquibaseRuntime files('src/main')
	
	testCompile('org.springframework.boot:spring-boot-starter-test')
}


liquibase {
	activities {
		main {
			driver 'com.mysql.cj.jdbc.Driver'
			url 'jdbc:mysql://localhost:3306/springdb'
			username 'root'
			password 'password'
			referenceUrl 'hibernate:spring:com.reading.tvirdee' +
					'?dialect=org.hibernate.dialect.MySQL5Dialect' +
					'&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy' +
					'&hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy'
			changeLogFile 'src/main/resources/db/changelog/changelog-master.yml'
		}
	}
}